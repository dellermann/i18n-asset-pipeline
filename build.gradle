/*
 * build.gradle
 *
 * Copyright (c) 2014-2016, Daniel Ellermann
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


buildscript {
    ext {
        grailsVersion = project.grailsVersion
    }
    repositories {
        mavenLocal()
        maven { url 'https://repo.grails.org/grails/core' }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
    }
}

version '2.0.0-TEST'
group 'org.grails.plugins'

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.grails.grails-plugin'
apply plugin: 'org.grails.grails-plugin-publish'
apply plugin: 'org.grails.grails-gsp'
apply plugin: 'org.grails.grails-doc'

ext {
    grailsVersion = project.grailsVersion
    gradleWrapperVersion = project.gradleWrapperVersion
}

repositories {
    mavenLocal()
    maven { url 'https://repo.grails.org/grails/core' }
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

dependencies {
    compile 'org.springframework.boot:spring-boot-autoconfigure'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-logging'
    compile 'org.springframework.boot:spring-boot-starter-tomcat'
    compile "org.grails:grails-core:$grailsVersion" //The version is mandatory because the dependencies wont be inside the pom
    compile 'org.grails:grails-dependencies'
    compile 'org.grails:grails-web-boot'
    compile 'org.grails.plugins:asset-pipeline:3.1.0'

    console 'org.grails:grails-console'

    profile 'org.grails.profiles:web-plugin:3.1.3'

    testCompile 'org.grails:grails-plugin-testing'
    compile gradleApi()
    compile localGroovy()
}
task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}

groovydoc {
    docTitle = "I18n asset pipeline API v${version}"
    windowTitle = "I18n asset pipeline API v${version}"
}

grailsPublish {
    user = project.hasProperty('bintrayUser') \
        ? project.property('bintrayUser')
        : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') \
        ? project.property('bintrayApiKey')
        : System.getenv('BINTRAY_API_KEY')
    userOrg = 'amc-world'
    githubSlug = 'dellermann/i18n-asset-pipeline'
    license {
        name = 'Apache-2.0'
    }
    title = 'i18n-asset-pipeline'
    desc = 'asset-pipeline plugin to use localized messages in JavaScript.'
    developers = [dellermann: 'Daniel Ellermann']
}
